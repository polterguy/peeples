/*
 * Create the main widget showing users, and create new user button, etc.
 */





/*
 * Retrieving all access rights, such that we can databind our access rights editor.
 */
p5.auth.access.list
lambda2hyper:x:/-/*





/*
 * Creating a grid, displaying all users in system.
 */
create-widget:peeples-users
  parent:peeples-main-container
  class:row
  oninit

    /*
     * Initial databinding of grid.
     */
    micro.evaluate.file:@PEEPLES/helpers/databind-users.hl

  widgets
    div
      class:col-100
      widgets
        div
          class:right
          widgets
            div
              class:strip toolbar
              widgets

                /*
                 * Help button, opens up GitHub README.md file in new window.
                 */
                button
                  innerValue:@"<span class=""icon-question""></span>"
                  onclick

                    /*
                     * Opens up GitHub project page in a new window.
                     */
                    p5.web.send-javascript:@"window.open('https://github.com/polterguy/peeples','_blank')"

                /*
                 * Home button, leads user to main desktop.
                 */
                button
                  innerValue:@"<span class=""icon-home3""></span>"
                  onclick

                    /*
                     * Redirecting to root location.
                     */
                    p5.web.get-root-location
                    p5.web.set-location:x:/-?value

    /*
     * Main wrapper for creating new users.
     */
    div
      class:col-50
      widgets
        div
          class:shaded rounded air-inner
          widgets
            h4
              innerValue:Create new user
            micro.widgets.wizard-form:peeples-add-wizard
              container
                element:p
                widgets
              text:peeples-add-wizard-username
                info:Username
                .data-field:username
                onkeydown:@"if (event.keyCode == 13) {p5.$('peeples-create-user-btn').raise('onclick');return false;}"
                oninit

                  /*
                   * Making sure we set initial focus to new username textbox.
                   */
                  micro.page.set-focus:x:/../*/_event?value

              text:peeples-add-wizard-role
                info:Role
                .data-field:role
                onkeydown:@"if (event.keyCode == 13) {p5.$('peeples-create-user-btn').raise('onclick');return false;}"
                value:user
              text:peeples-add-wizard-pwd
                info:Password
                .data-field:password
                type:password
                onkeydown:@"if (event.keyCode == 13) {p5.$('peeples-create-user-btn').raise('onclick');return false;}"
                autocomplete:new-password
              text:peeples-add-wizard-pwd-repeat
                info:Repeat
                .data-field:password-repeat
                type:password
                onkeydown:@"if (event.keyCode == 13) {p5.$('peeples-create-user-btn').raise('onclick');return false;}"
                autocomplete:new-password
              checkbox
                info:Show password
                onchange

                  /*
                   * Getting value of checkbox, and toggling password/text type of password textboxes accordingly.
                   */
                  get-widget-property:x:/../*/_event?value
                    checked
                  if:x:/-/*/*
                    set-widget-property:peeples-add-wizard-pwd
                      type:text
                    set-widget-property:peeples-add-wizard-pwd-repeat
                      type:text
                  else
                    set-widget-property:peeples-add-wizard-pwd
                      type:password
                    set-widget-property:peeples-add-wizard-pwd-repeat
                      type:password

              div
                class:right
                widgets

                  /*
                   * Create new user button.
                   */
                  button:peeples-create-user-btn
                    title:Create new user
                    class:larger
                    innerValue:Add
                    onclick

                      /*
                       * Serializing wizard form, and creating user, making sure
                       * we do it within a try/catch block, in case of exceptions.
                       */
                      micro.form.serialize:peeples-add-wizard

                      /*
                       * Sanity check, verifying passwords are a match.
                       */
                      if:x:/@micro.form.serialize/*(/password|/password-repeat)/=$?count
                        !=:int:1

                        /*
                         * Passwords are not a match, informing user, and returning early.
                         */
                        micro.windows.info:Passwords doesn't match
                          class:micro-windows-info warning
                        return

                      try

                        /*
                         * Creating user.
                         */
                        p5.auth.users.create:x:/@micro.form.serialize/*/username?value
                          role:x:/@micro.form.serialize/*/role?value
                          password:x:/@micro.form.serialize/*/password?value

                        /*
                         * Re-databinding users grid.
                         */
                        micro.evaluate.file:@PEEPLES/helpers/databind-users.hl

                        /*
                         * Emptying form.
                         */
                        _widgets
                          peeples-add-wizard-username
                          peeples-add-wizard-pwd
                          peeples-add-wizard-pwd-repeat
                        set-widget-property:x:/-/*?name
                          value:

                        /*
                         * Setting focus to username widget, and providing feedback to user.
                         */
                        micro.page.set-focus:peeples-add-wizard-username
                        micro.windows.info:User was successfully created
                          class:micro-windows-info success

                      catch

                        /*
                         * Displaying error to user.
                         */
                        micro.windows.info:x:/..catch/*/message?value
                          class:micro-windows-info warning

    /*
     * Main wrapper for displaying existing users.
     */
    div
      class:col-50
      widgets
        div
          class:shaded rounded air-inner
          widgets
            micro.widgets.grid:peeples-grid
              class:hover
              columns
                Username
                Role
                Delete
                  style:"float:right;"

            /*
             * Pager button wrapper.
             */
            div
              class:right
              widgets
                div
                  .offset:0
                  class:strip
                  widgets
                    button:peeples-previous-btn
                      innerValue:<
                      onclick

                        /*
                         * Paging to previous page.
                         */
                        p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                          .offset
                        get-widget-property:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                          .offset
                        -:x:/@get-widget-property/*/*?value.int
                          _:int:10
                        set-widget-property:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                          .offset:x:/@-?value

                        /*
                         * For simplicity, simply invoking main launch file again,
                         * now with offset incremented by 10.
                         */
                        eval-x:x:/+/*
                        micro.evaluate.file:@PEEPLES/helpers/databind-users.hl
                          offset:x:/@-?value

                    button:peeples-next-btn
                      innerValue:>
                      onclick

                        /*
                         * Paging to next page.
                         */
                        p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                          .offset
                        get-widget-property:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                          .offset
                        +:x:/@get-widget-property/*/*?value.int
                          _:int:10
                        set-widget-property:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                          .offset:x:/@+?value

                        /*
                         * For simplicity, simply invoking main launch file again,
                         * now with offset incremented by 10.
                         */
                        eval-x:x:/+/*
                        micro.evaluate.file:@PEEPLES/helpers/databind-users.hl
                          offset:x:/@+?value

    /*
     * Main wrapper for editing access rights, or authorization objects.
     */
    div
      class:col-100
      widgets
        div:peeples-access-right
          class:shaded rounded air-inner
          widgets
            label
              innerValue:Access rights (authorization objects)
            micro.widgets.codemirror:peeples.access-rights
              mode:hyperlambda
              value:x:/../*/lambda2hyper?value
              .data-field:access
            div
              class:right air-top
              widgets
                div
                  class:strip
                  widgets

                    /*
                     * Save access rights object.
                     */
                    button
                      title:Save access objects
                      class:larger
                      innerValue:Save
                      onclick

                        /*
                         * Catching exceptions to give friendly feedback to user in case of an exception.
                         */
                        try

                          /*
                           * Retrieves access objects, and saves them to auth file.
                           */
                          micro.form.serialize:peeples-access-right

                          /*
                           * Checking that user actually supplied something, and not just an empty piece of text.
                           *
                           * Notice, we must support empty string, to allow for deleting all existing access objects,
                           * without creating any new ones.
                           */
                          if:x:/@micro.form.serialize/*/access?value
                            !=:

                            /*
                             * User actually supplied something new.
                             */
                            hyper2lambda:x:/@micro.form.serialize/*/access?value
                            add:x:/..try/*/p5.auth.access.set-all
                              src:x:/@hyper2lambda/*

                          /*
                           * Updating access to whatever new value(s) the user provided, and providing some feedback
                           * to user.
                           */
                          p5.auth.access.set-all
                          micro.windows.info:Access objects succsessfully saved
                            class:micro-windows-info success

                        catch

                          /*
                           * Displaying friendly feedback to user.
                           */
                          micro.windows.info:x:/@message?value
                            class:micro-windows-info warning

                    /*
                     * Grant access to module (guided version).
                     */
                    button
                      title:Grant access to module for specified role
                      innerValue:@"<span class=""icon-plus""></span>"
                      onclick

                        /*
                         * Allows user to grant access to a module, for some specific role (or all roles).
                         * Shows a modal widget, allowing user to select module, and role.
                         */
                        create-widgets
                          micro.widgets.modal:peeples.grant-access-wizard
                            widgets
                              h3
                                innerValue:Grant access
                              p
                                innerValue:Choose a module, and a role.
                              micro.widgets.wizard-form
                                select
                                  info:Module
                                  .data-field:access-module
                                  oninit

                                    /*
                                     * Create one option element for each module in the system
                                     * that has a desktop icon.
                                     */
                                    list-folders:/modules/
                                    for-each:x:/@list-folders/*?name
                                      split:x:/@_dp?value
                                        =:/
                                      create-widget
                                        element:option
                                        parent:x:/../*/_event?value
                                        innerValue:x:/@split/1?name
                                        value:{0}
                                          :x:/@_dp?value

                                select
                                  info:Role
                                  .data-field:access-role
                                  oninit

                                    /*
                                     * Create one option element for each role in system.
                                     */
                                    p5.auth.roles.list
                                    for-each:x:/@p5.auth.roles.list/*?name
                                      create-widget
                                        element:option
                                        parent:x:/../*/_event?value
                                        innerValue:x:/@_dp?value
                                        value:x:/@_dp?value

                                  widgets
                                    option
                                      innerValue:All roles (*)
                                      value:*

                                div
                                  class:right
                                  widgets
                                    div
                                      class:strip
                                      widgets
                                        button
                                          innerValue:Add
                                          onclick

                                            /*
                                             * Catching exceptions to give friendly feedback to user in case of an exception.
                                             */
                                            try

                                              /*
                                               * Retrieves role and module, and creates a new access object to allow
                                               * role access to module.
                                               */
                                              micro.form.serialize:cnt
                                              hyper2lambda:x:/@micro.form.serialize/*/access?value
                                              add:x:/..try/*/p5.auth.access.add
                                                src:x:/@micro.form.serialize/*/access-role?value
                                              eval-x:x:/+/*/*
                                              add:x:/..try/*/p5.auth.access.add/0
                                                src
                                                  p5.module.allow:x:/@micro.form.serialize/*/access-module?value
                                              p5.auth.access.add

                                              /*
                                               * Updating access object widget.
                                               */
                                              p5.web.widgets.find-first:peeples.access-rights
                                                element:textarea
                                              p5.auth.access.list
                                              lambda2hyper:x:/@p5.auth.access.list/*
                                              set-widget-property:x:/@p5.web.widgets.find-first/*/*?value
                                                value:x:/@lambda2hyper?value
                                              p5.web.send-javascript:@"p5['{0}'].getDoc().setValue(`{1}`);"
                                                :x:/@p5.web.widgets.find-first/*/*?value
                                                :x:/@lambda2hyper?value

                                              /*
                                               * Deleting modal widget, and providing feedback to user.
                                               */
                                              delete-widget:peeples.grant-access-wizard
                                              micro.windows.info:Your access objects were successfully updated
                                                class:micro-windows-info success

                                            catch

                                              /*
                                               * Displaying friendly feedback to user.
                                               */
                                              micro.windows.info:x:/@message?value
                                                class:micro-windows-info warning

                                        button
                                          innerValue:Cancel
                                          onclick

                                            /*
                                             * Simply deleting modal widget.
                                             */
                                            delete-widget:peeples.grant-access-wizard
