
/*
 * Create the main widget showing users, and create new user button, etc.
 */





/*
 * Creating a grid, displaying all users in system.
 */
create-widget:peeples-users
  parent:peeples-main-container
  class:row
  oninit

    /*
     * Initial databinding of grid.
     */
    micro.evaluate.file:@PEEPLES/helpers/databind-users.hl

  widgets
    div
      class:col-100
      widgets
        div
          class:right
          widgets
            div
              class:strip
              style:"display:inline-block;"
              widgets

                /*
                 * Settings, opens up settings form.
                 */
                button
                  innerValue:@"<span class=""icon-cog""></span>"
                  title:Open up settings
                  onclick

                    /*
                     * Evaluating file responsible for opening up settings.
                     */
                    micro.evaluate.file:@PEEPLES/helpers/configure.hl

                /*
                 * Home button, leads user to main desktop.
                 */
                button
                  innerValue:@"<span class=""icon-home""></span>"
                  onclick

                    /*
                     * Redirecting to root location.
                     */
                    p5.web.get-root-location
                    p5.web.set-location:x:/-?value

    /*
     * Main wrapper for creating new users.
     */
    div
      class:col-50
      widgets
        div
          class:shaded rounded air-inner
          widgets
            micro.widgets.wizard-form:peeples-add-wizard
              container
                element:p
                widgets
                  literal:peeples-tickets-left
                    element:span
                    oninit

                      /*
                       * Updating available tickets count.
                       */
                      peeples.tickets-update

                    events

                      /*
                       * Updates available tickets count.
                       */
                      peeples.tickets-update

                        /*
                         * Checking how many user tickets there are left in the system.
                         */
                        peeples.tickets.available

                        /*
                         * Displaying number of available user tickets.
                         */
                        set-widget-property:x:/../*/_event?value
                          innerValue:@"You have {0} user tickets left."
                            :x:/@peeples.tickets.available?value

                  literal
                    element:a
                    href:#
                    innerValue:Purchase additional tickets.
                    onclick

                      /*
                       * Invoking file responsible for purchasing more tickets.
                       */
                      micro.evaluate.file:@PEEPLES/helpers/purchase-users.hl

              text:peeples-add-wizard-username
                info:Username
                .data-field:username
                onkeydown:@"if (event.keyCode == 13) {p5.$('peeples-create-user-btn').raise('onclick');return false;}"
                oninit

                  /*
                   * Making sure we set initial focus to username texbox.
                   */
                  micro.page.set-focus:x:/../*/_event?value

              text:peeples-add-wizard-role
                info:Role
                .data-field:role
                onkeydown:@"if (event.keyCode == 13) {p5.$('peeples-create-user-btn').raise('onclick');return false;}"
                value:user
              text:peeples-add-wizard-pwd
                info:Password
                .data-field:password
                type:password
                onkeydown:@"if (event.keyCode == 13) {p5.$('peeples-create-user-btn').raise('onclick');return false;}"
              text:peeples-add-wizard-pwd-repeat
                info:Repeat
                .data-field:password-repeat
                type:password
                onkeydown:@"if (event.keyCode == 13) {p5.$('peeples-create-user-btn').raise('onclick');return false;}"
              div
                class:right
                widgets

                  /*
                   * Create new user button.
                   */
                  button:peeples-create-user-btn
                    title:Create new user
                    style:"margin-bottom:0;width:120px;"
                    innerValue:Save
                    onclick

                      /*
                       * Verifying user hasn't created more users already than what he
                       * is allowed to, according to his configuration settings.
                       */
                      p5.auth.users.list
                      load-file:@PEEPLES/configuration/config.hl
                      if:x:/@p5.auth.users.list/*?count
                        >=:x:/@load-file/*/*/tix?value.int

                        /*
                         * User has exceeded his license terms, invoking file responsible
                         * for allowing him to purchase additional users, and returning early.
                         */
                        micro.evaluate.file:@PEEPLES/helpers/purchase-users.hl
                        return

                      /*
                       * Serializing wizard form, and creating user, making sure
                       * we do it within a try/catch block, in case of exceptions.
                       */
                      micro.widgets.wizard-form.value:peeples-add-wizard

                      /*
                       * Sanity check, verifying passwords are a match.
                       */
                      if:x:/@micro.widgets.wizard-form.value/*(/password|/password-repeat)/=$?count
                        !=:int:1

                        /*
                         * Passwords are not a match, informing user, and returning early.
                         */
                        micro.windows.info:Passwords doesn't match
                          class:micro-windows-info warning
                        return

                      try

                        /*
                         * Creating user.
                         */
                        eval-x:x:/+/*
                        p5.auth.users.create:x:/@micro.widgets.wizard-form.value/*/username?value
                          role:x:/@micro.widgets.wizard-form.value/*/role?value
                          password:x:/@micro.widgets.wizard-form.value/*/password?value

                        /*
                         * Re-databinding users grid.
                         */
                        micro.evaluate.file:@PEEPLES/helpers/databind-users.hl

                        /*
                         * Emptying form.
                         */
                        _widgets
                          peeples-add-wizard-username
                          peeples-add-wizard-pwd
                          peeples-add-wizard-pwd-repeat
                        set-widget-property:x:/-/*?name
                          value:

                        /*
                         * Updating available tickets count, since it's now reduced by one.
                         */
                        peeples.tickets-update

                        /*
                         * Setting focus to username widget, and providing feedback to user.
                         */
                        micro.page.set-focus:peeples-add-wizard-username
                        micro.windows.info:User was successfully created
                          class:micro-windows-info success

                      catch

                        /*
                         * Displaying error to user.
                         */
                        micro.windows.info:x:/..catch/*/message?value
                          class:micro-windows-info warning

    /*
     * Main wrapper for displaying existing users.
     */
    div
      class:col-50
      widgets
        div
          class:shaded rounded air-inner
          widgets
            micro.widgets.grid:peeples-grid
              class:hover
              columns
                Username
                Role
                Delete
                  style:"float:right;"

            /*
             * Pager button wrapper.
             */
            div
              class:right
              widgets
                div
                  .offset:0
                  class:strip
                  style:"display:inline-block;"
                  widgets
                    button:peeples-previous-btn
                      innerValue:&lt;
                      style:"margin-bottom:0;"
                      onclick

                        /*
                         * Paging to previous page.
                         */
                        p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                          .offset
                        get-widget-property:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                          .offset
                        -:x:/@get-widget-property/*/*?value.int
                          _:int:10
                        set-widget-property:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                          .offset:x:/@-?value

                        /*
                         * For simplicity, simply invoking main launch file again,
                         * now with offset incremented by 10.
                         */
                        eval-x:x:/+/*
                        micro.evaluate.file:@PEEPLES/helpers/databind-users.hl
                          offset:x:/@-?value

                    button:peeples-next-btn
                      innerValue:&gt;
                      style:"margin-bottom:0;"
                      onclick

                        /*
                         * Paging to next page.
                         */
                        p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                          .offset
                        get-widget-property:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                          .offset
                        +:x:/@get-widget-property/*/*?value.int
                          _:int:10
                        set-widget-property:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                          .offset:x:/@+?value

                        /*
                         * For simplicity, simply invoking main launch file again,
                         * now with offset incremented by 10.
                         */
                        eval-x:x:/+/*
                        micro.evaluate.file:@PEEPLES/helpers/databind-users.hl
                          offset:x:/@+?value
