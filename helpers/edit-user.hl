/*
 * This file is responsible for editing an existing user.
 *
 * Requires __[username]__ being the username of user to edit.
 */





/*
 * Checking username, and disallowing editing of "root" account.
 */
if:x:/../*/username?value
  =:root

  /*
   * The system "root" account is not possible to edit!
   */
  micro.windows.info:Sorry, the system root account cannot be edited
    class:micro-windows-info warning
  return





/*
 * Retrieving user's essential information.
 */
p5.auth.users.get:x:/../*/username?value





/*
 * Creating a modal widget which allows the user to edit the currently selected user.
 */
eval-x:x:/+/*/*/*/micro.widgets.wizard-form/*/value
create-widgets
  micro.widgets.modal:peeples-edit-user-modal
    widgets
      h3
        innerValue:Edit user
      micro.widgets.wizard-form:peeples-edit-user-wizard

        /*
         * Username textbox, notice this widget is "readonly".
         */
        text:peeples-edit-username
          info:Username
          .data-field:username
          value:x:/../*/username?value
          readonly:true
          title:The username cannot be changed once the user has been created

        /*
         * User's role textbox.
         */
        text:peeples-edit-role
          info:Role
          .data-field:role
          value:x:/@p5.auth.users.get/*/*/role?value
          onkeydown:@"if (event.keyCode == 13) {p5.$('peeples-edit-user-save-btn').raise('onclick');return false;}"
          oninit

            /*
             * Making sure we set initial focus to user's role.
             */
            micro.page.set-focus:x:/../*/_event?value

        hr
        p
          innerValue:If you leave the password blank, it will not be changed.

        /*
         * User's password textbox.
         */
        text:peeples-edit-password
          info:Password
          .data-field:password
          type:password
          onkeydown:@"if (event.keyCode == 13) {p5.$('peeples-edit-user-save-btn').raise('onclick');return false;}"
          title:If you leave the password blank, the password will not be changed
          autocomplete:new-password

        /*
         * User's password repeat textbox.
         */
        text:peeples-edit-password-repeat
          info:Repeat
          .data-field:password-repeat
          type:password
          onkeydown:@"if (event.keyCode == 13) {p5.$('peeples-edit-user-save-btn').raise('onclick');return false;}"
          title:If you leave the password blank, the password will not be changed
          autocomplete:new-password

        /*
         * Checkbox that allows user to explicitly see password(s) from above.
         */
        checkbox
          info:Show password
          onchange

            /*
             * Getting value of checkbox, and toggling password/text type of password textboxes accordingly.
             */
            get-widget-property:x:/../*/_event?value
              checked
            if:x:/-/*/*
              set-widget-property:peeples-edit-password
                type:text
              set-widget-property:peeples-edit-password-repeat
                type:text
            else
              set-widget-property:peeples-edit-password
                type:password
              set-widget-property:peeples-edit-password-repeat
                type:password


      /*
       * Wrapper for "Save" and "Close" buttons.
       */
      div
        class:right
        widgets
          div
            class:strip
            widgets
              button:peeples-edit-user-save-btn
                innerValue:Save
                onclick

                  /*
                   * Creating a cover widget, since editing s user might take time,
                   * if his password is being changed.
                   */
                  create-widgets
                    micro.widgets.cover:peeples-cover-widget
                      message:Please wait while I update the user ...
                  p5.web.send-javascript:@"p5.$('peeples-edit-user-save-btn').raise('.onclick');"

                .onclick

                  /*
                   * Deleting cover widget.
                   */
                  delete-widget:peeples-cover-widget

                  /*
                   * Wrapping everything inside a trye/catch block, in case an exception occurs.
                   */
                  try

                    /*
                     * Serializing wizard form, updating user, and closing modal widget.
                     */
                    micro.form.serialize:peeples-edit-user-wizard

                    /*
                     * Verifying passwords, if given, are matching.
                     */
                    if:x:/@micro.form.serialize/*(/password|/password-repeat)/=$?count
                      !=:int:1

                      /*
                       * A new password was given, and the passwords did not match.
                       */
                      micro.windows.info:The passwords did not match
                        class:micro-windows-info warning
                      return

                    /*
                     * Updating user.
                     *
                     * Notice, if password is empty, it won't be changed.
                     *
                     * Also notice, in order to preserver user's old settings, we'll
                     * need to fetch them explicitly, and append them into our update invocation.
                     */
                    p5.auth.users.get:x:/@micro.form.serialize/*/username?value

                    /*
                     * Adding role, which might have changed.
                     */
                    add:x:/..try/*/p5.auth.users.edit
                      src:x:/@micro.form.serialize/*/role

                    /*
                     * Checking if password was left non-blank.
                     */
                    if:x:/@micro.form.serialize/*/password?value
                      !=:

                      /*
                       * Password was not empty, making sure we add it to edit invocation.
                       */
                      add:x:/..try/*/p5.auth.users.edit
                        src:x:/@micro.form.serialize/*/password

                    /*
                     * Adding all other user specific settings.
                     */
                    add:x:/..try/*/p5.auth.users.edit
                      src:x:/..try/*/p5.auth.users.get/*/*(!/role!/password)

                    /*
                     * Updating user.
                     */
                    p5.auth.users.edit:x:/@micro.form.serialize/*/username?value

                    /*
                     * Closing modal window and displaying some information to user.
                     */
                    delete-widget:peeples-edit-user-modal
                    micro.windows.info:User was successfully updated
                      class:micro-windows-info success

                    /*
                     * Re-databinding users grid.
                     */
                    micro.evaluate.file:@PEEPLES/helpers/databind-users.hl

                  catch

                    /*
                     * Displaying error to user.
                     */
                    micro.windows.info:x:/@message?value
                      class:micro-windows-info warning

              button
                innerValue:Close
                onclick

                  /*
                   * Deleting modal widget.
                   */
                  delete-widget:peeples-edit-user-modal
