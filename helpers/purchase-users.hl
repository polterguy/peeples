
/*
 * Allows user to purchase more users.
 */
create-widgets
  micro.widgets.modal:peeples-purchase-users
    class:micro-modal micro-modal-smaller
    .onpaypal-ok

      /*
       * Loading configuration file, adding newly acquired tickets, 
       * and saving it again.
       */
      get-widget-property:peeples-no-users
        value
      load-file:@PEEPLES/configuration/config.hl
      set:x:/@load-file/*/*/tix?value
        +:x:/@load-file/*/*/tix?value.int
          _:x:/@get-widget-property/*/*?value.int
      p5.crypto.hash.create-sha256:x:/@load-file/*/*/tix?value.string
      set:x:/@load-file/*/*/hash?value
        src:x:/@p5.crypto.hash.create-sha256?value
      lambda2hyper:x:/@load-file/*/*
      save-file:@PEEPLES/configuration/config.hl
        src:x:/@lambda2hyper?value

      /*
       * Deleting modal widget.
       */
      delete-widget:peeples-purchase-users

      /*
       * Updating available tickets.
       */
      peeples.tickets-update

    widgets
      h3
        innerValue:Purchase additional user tickets?
      p
        innerValue:@"Choose how many additional user tickets you want to purchase. When you click OK, you will be taken to PayPal to finish your purchase.
Having troubles? Shoot me an email at <a href=""mailto:thomas@gaiasoul.com"">thomas@gaiasoul.com</a>."
      p
        style:"font-weight:bold;"
        widgets
          span
            innerValue:"Total price "
          span:peeples-total-price
            innerValue:
      micro.widgets.wizard-form:peeples-purchase-new-users
        select:peeples-no-users
          info:Tickets
          .data-field:users
          oninit
            p5.web.widgets.ajax-events.raise:peeples-no-users
              onchange
          onchange

            /*
             * Selecting Peeples configuration settings, to determine pricing, and
             * making sure we display it to the end user.
             */
            select-data:x:/*/*/peeples.configuration
            _price:5.0
            _factor:1.0
            _multiply:1.0
            _qty-discount:1.0

            /*
             * Checking if there exists a [multiply] configuration setting.
             */
            if:x:/@select-data/*/*/multiply?value

              /*
               * Changing [_multiply] accordingly.
               *
               * Notice, this is the [0>1.0] multiplication factor, as defined
               * by the user's coupon code.
               */
              set:x:/@_multiply?value
                src:x:/@select-data/*/*/multiply?value

            /*
             * Checking if this is a professional installation.
             */
            if:x:/@select-data/*/*/type?value
              =:professional
              set:x:/@_price?value
                src:50.0

            /*
             * Checking if this is a Norwegian government installation.
             */
            if:x:/@select-data/*/*/norwegian-government?value
              set:x:/@_factor?value
                src:3.14

            /*
             * Figuring out volume discount.
             */
            get-widget-property:peeples-no-users
              value
            switch:x:/@get-widget-property/*/*?value
              case:10
                set:x:/@_qty-discount?value
                  src:0.95
              case:25
                set:x:/@_qty-discount?value
                  src:0.90
              case:50
                set:x:/@_qty-discount?value
                  src:0.80
              case:100
                set:x:/@_qty-discount?value
                  src:0.70
              case:250
                set:x:/@_qty-discount?value
                  src:0.60
              case:500
                set:x:/@_qty-discount?value
                  src:0.50

            /*
             * Calculating total.
             */
            _total
            set:x:/@_total?value
              *:x:/@get-widget-property/*/*?value.decimal
                _:x:/@_price?value
                _:x:/@_factor?value
                _:x:/@_qty-discount?value
                _:x:/@_multiply?value

            /*
             * Setting total price.
             */
            set-widget-property:peeples-total-price
              innerValue:"â‚¬{0:C}"
                :x:/@_total?value

          options
            5:5
            10:10
            25:25
            50:50
            100:100
            250:250
            500:500
      div
        class:right
        widgets
          container:paypal-button
            oninit

              /*
               * Retrieving configuration, and passing into rendering of PayPal button.
               *
               * Notice, we will modify parameters according to how Peeples was configured.
               */
              select-data:x:/*/*/peeples.configuration
              _price:5.0
              _factor:1
              _multiply:1.0
              _sales-rep:nobody

              /*
               * Checking if there exists a [multiply] configuration setting.
               */
              if:x:/@select-data/*/*/multiply?value

                /*
                 * Changing [_multiply] accordingly.
                 *
                 * Notice, this is the [0>1.0] multiplication factor, as defined
                 * by the user's coupon code.
                 *
                 * If there exists a [multiply], there also exist a [sales-rep].
                 * Hence we also set the [_sales-rep] value here.
                 */
                set:x:/@_multiply?value
                  src:x:/@select-data/*/*/multiply?value
                set:x:/@_sales-rep?value
                  src:x:/@select-data/*/*/sales-rep?value

              /*
               * Checking if this is a professional installation.
               */
              if:x:/@select-data/*/*/type?value
                =:professional
                set:x:/@_price?value
                  src:50.0

              /*
               * Checking if this is a Norwegian government installation.
               */
              if:x:/@select-data/*/*/norwegian-government?value
                set:x:/@_factor?value
                  src:3.14

              /*
               * Rendering PayPal button accordingly.
               */
              p5.web.send-javascript:@"
paypal.Button.render({{
  env: 'production',
  client:{{
    sandbox:'ASS9zCiAjKx2cW_Gy234SW5qfX2mWlRoQZDelsokl2Dk2RFX9WeJ3yF5bPu9v_Ajcqayy6zdvLFu-0g0',
    production:'AYXmWWv-OKV_RFjcksDoK4nJ0djwlhCmXp0h9staoD4U9dsY0oKfw8PBPH9TJ68s0SHjqGzWJNOT0inv'}},
  commit: true,
  style:{{
    size: 'small',
    color: 'gold',
    shape: 'pill',
    label: 'checkout'}},
  payment: function(data, actions){{
    var unit_price = {0};
    var units = p5.$('peeples-no-users').el.value;
    var factor = {1};
    var multiply = {2};
    var qty_discount = 1.0;
    switch(p5.$('peeples-no-users').el.value){{
      case '10':{{
        qty_discount = 0.95;
        break;}}
      case '25':{{
        qty_discount = 0.90;
        break;}}
      case '50':{{
        qty_discount = 0.80;
        break;}}
      case '100':{{
        qty_discount = 0.70;
        break;}}
      case '250':{{
        qty_discount = 0.60;
        break;}}
      case '500':{{
        qty_discount = 0.50;
        break;}}
    }}
    return actions.payment.create({{
      payment:{{
        transactions:[{{
          amount:{{
            total:unit_price * units * qty_discount * factor * multiply,currency: 'EUR'}}, 
            custom:'sales-rep - {3}'}}]
      }}
    }});
  }},
  onAuthorize: function(data, actions){{
    return actions.payment.execute().then(function(payment){{
      p5.$('peeples-purchase-users').raise('.onpaypal-ok');}});
  }}
}}, '#paypal-button');"
                :x:/@_price?value
                :x:/@_factor?value
                :x:/@_multiply?value
                :x:/@_sales-rep?value
