
/*
 * Desktop widget file for Peeples.
 */
container
  element:a
  class:desktop-app shaded rounded air-inner bg
  title:Manage users
  oninit

    /*
     * Verifying user is logged in as root, and if not, deleting widget altogether, 
     * and returning early.
     */
    whoami
    if:x:/@whoami/*/role?value
      !=:root

      /*
       * User is not logged in as root, simply deleting entire icon.
       */
      delete-widget:x:/../*/_event?value
      return

    /*
     * Getting URL to point correctly.
     */
    p5.io.unroll-path:@PEEPLES/
    split:x:/-?value
      =:/
    set-widget-property:x:/../*/_event?value
      href:/{0}
        :x:/@split/0/-?name

  widgets
    literal
      element:span
      class:desktop-app-name
      oninit

        /*
         * Verifying user is root, before allowing lambda to continue.
         */
        whoami
        if:x:/@whoami/*/role?value
          !=:root

          /*
           * User is not logged in as root, simply deleting entire icon.
           */
          return

        /*
         * Making sure we change main widget's innerValue to show the number of users
         * in the system.
         */
        p5.auth.users.list
        set-widget-property:x:/../*/_event?value
          innerValue:@"<span class=""icon-users desktop-app-icon""></span><span style=""position:absolute;top:5px;right:5px;font-size:.8rem;"">{0}</span>"
            :x:/@p5.auth.users.list/*?count
