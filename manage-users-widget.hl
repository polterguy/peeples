
/*
 * Create the main widget showing users, and create new user button, etc.
 *
 * Optionally pass in [offset], which defaults to "0".
 */
.defaults
  offset:int:0





/*
 * Listing all users in system.
 */
p5.auth.users.list





/*
 * Helper for [apply] below.
 */
.apply
  add:x:/../*/return
    src:@"Username:{0}"
      :x:/@_dn/#?name
  add:x:/../*/return
    src:@"Role:{0}"
      :x:/@_dn/#?value
  if:x:/@_dn/#?name
    =:root

    /*
     * For security reasons, we don't allow deletion of main "root" account.
     */
    set:x:/../*/add/**/Delete/*/widgets/*/a/*/onclick
    add:x:/../*/add/**/Delete/*/widgets/*/a
      src
        onclick
          micro.windows.info:Sorry, this account cannot be deleted!
            class:micro-windows-info warning

  eval-x:x:/+/**/_username
  add:x:/../*/return
    src
      Delete
        widgets
          a
            href:#
            role:button
            style:"float:right;"
            innerValue:@"<span class=""icon-trash""></span>"
            title:Delete user
            onclick

              /*
               * Asks user to confirm deletion of user.
               */
              _username:x:/@_dn/#?name
              eval-x:x:/+/*/*/*/h3/*/innerValue
              create-widgets
                micro.widgets.modal:peeples-confirm-deletion-of-user
                  widgets
                    h3
                      innerValue:Confirm deletion of '{0}'
                        :x:/@_username?value
                    p
                      innerValue:Are you sure you want to delete this user? This action will delete all of user's files, and is final.
                    div
                      class:right
                      widgets
                        div
                          class:strip
                          style:"display:inline-block;"
                          widgets
                            button
                              style:"margin-bottom:0;"
                              innerValue:Yes
                              onclick

                                /*
                                 * Deletes user and re-evaluates launch file for
                                 * simplicity.
                                 */
                                _username:x:/@_dn/#?name
                                p5.auth.users.delete:x:/@_username?value
                                micro.evaluate.file:@PEEPLES/launch.hl

                            button
                              style:"margin-bottom:0;"
                              innerValue:No
                              oninit

                                /*
                                 * Making sure "No" button has initial focus.
                                 */
                                micro.page.set-focus:x:/../*/_event?value

                              onclick

                                /*
                                 * Deletes modal widget.
                                 */
                                delete-widget:peeples-confirm-deletion-of-user

  /*
   * Returning row to caller, forward evaluating [_username] inside of onclick
   * of row first.
   */
  eval-x:x:/+/*/.row/**/_username
  return
    .row
      onclick

        /*
         * Evaluates file responsible for editing existing user.
         */
        _username:x:/@_dn/#?name
        eval-x:x:/+/*
        micro.evaluate.file:@PEEPLES/edit-user.hl
          username:x:/@_username?value





/*
 * Calculating which sub-section of users we should display.
 */
+:x:/../*(/offset|/.defaults/*/offset)/$?value.int
  _:int:10





/*
 * Checking if the "next" button should be disabled.
 */
if:x:/@p5.auth.users.list/*?count
  <=:x:/@+?value.int

  /*
   * Next button should be disabled.
   */
  add:x:@"/../*/create-widget/**/innerValue/""=&gt;""/."
    src:disabled





/*
 * Actually databinding our grid.
 */
apply:x:/../*/create-widget/**/micro.widgets.grid/*/rows
  src:x:/@p5.auth.users.list/*/[{0},{1}]
    :x:/../*(/offset|/.defaults/*/offset)/$?value.int
    :x:/@+?value
  template
    item
      {@eval}:x:/@.apply





/*
 * Creating a grid, displaying all users in system.
 */
eval-x:x:/+/**/.offset
create-widget:peeples-users
  parent:peeples-main-container
  class:row
  widgets
    div
      class:col-100
      widgets
        div
          class:right
          widgets
            button
              innerValue:@"<span class=""icon-close""></span>"
              onclick

                /*
                 * Redirecting to root location.
                 */
                p5.web.get-root-location
                p5.web.set-location:x:/-?value

    /*
     * Main wrapper for creating new users.
     */
    div
      class:col-50
      widgets
        div
          class:shaded rounded air-inner
          widgets
            micro.widgets.wizard-form:peeples-add-wizard
              h3
                innerValue:Create new user
              container
                element:p
                widgets
                  literal
                    element:span
                    oninit

                      /*
                       * Checking how many user tickets there are left in the system.
                       */
                      p5.auth.users.list
                      load-file:@PEEPLES/configuration/config.hl
                      -:x:/@load-file/*/*/tickets?value.int
                        _:x:/@p5.auth.users.list/*?count

                      /*
                       * Displaying number of user tickets that areleft in the system.
                       */
                      set-widget-property:x:/../*/_event?value
                        innerValue:@"You have {0} user tickets left."
                          :x:/@-?value

                  literal
                    element:a
                    href:#
                    innerValue:Purchase additional tickets.
                    onclick

                      /*
                       * Invoking file responsible for purchasing more tickets.
                       */
                      micro.evaluate.file:@PEEPLES/purchase-users.hl

              text:peeples-add-wizard-username
                info:Username
                .data-field:username
                onkeydown:@"if (event.keyCode == 13) {p5.$('peeples-create-user-btn').raise('onclick');return false;}"
                oninit

                  /*
                   * Making sure we set initial focus to username texbox.
                   */
                  micro.page.set-focus:x:/../*/_event?value

              text:peeples-add-wizard-role
                info:Role
                .data-field:role
                onkeydown:@"if (event.keyCode == 13) {p5.$('peeples-create-user-btn').raise('onclick');return false;}"
                value:user
              p
                innerValue:Type in initial password for new user twice.
              text:peeples-add-wizard-pwd
                info:Password
                .data-field:password
                type:password
                onkeydown:@"if (event.keyCode == 13) {p5.$('peeples-create-user-btn').raise('onclick');return false;}"
              text:peeples-add-wizard-pwd-repeat
                info:Repeat
                .data-field:password-repeat
                type:password
                onkeydown:@"if (event.keyCode == 13) {p5.$('peeples-create-user-btn').raise('onclick');return false;}"
              div
                class:right
                widgets

                  /*
                   * Create new user button.
                   */
                  button:peeples-create-user-btn
                    title:Create new user
                    style:"margin-bottom:0;width:120px;"
                    innerValue:@"<span class=""icon-plus""></plus>"
                    onclick

                      /*
                       * Verifying user hasn't created more users already than what he
                       * is allowed to, according to his configuration settings.
                       */
                      p5.auth.users.list
                      load-file:@PEEPLES/configuration/config.hl
                      if:x:/@p5.auth.users.list/*?count
                        >=:x:/@load-file/*/*/tickets?value.int

                        /*
                         * User has exceeded his license terms, invoking file responsible
                         * for allowing him to purchase additional users, and returning early.
                         */
                        micro.evaluate.file:@PEEPLES/purchase-users.hl
                        return

                      /*
                       * Serializing wizard form, and creating user, making sure
                       * we do it within a try/catch block, in case of exceptions.
                       */
                      micro.widgets.wizard-form.value:peeples-add-wizard

                      /*
                       * Sanity check, verifying passwords are a match.
                       */
                      if:x:/@micro.widgets.wizard-form.value/*(/password|/password-repeat)/=$?count
                        !=:int:1

                        /*
                         * Passwords are not a match, informing user, and returning early.
                         */
                        micro.windows.info:Passwords doesn't match
                          class:micro-windows-info warning
                        return

                      try

                        /*
                         * Creating user.
                         */
                        eval-x:x:/+/*
                        p5.auth.users.create:x:/@micro.widgets.wizard-form.value/*/username?value
                          role:x:/@micro.widgets.wizard-form.value/*/role?value
                          password:x:/@micro.widgets.wizard-form.value/*/password?value

                      catch

                        /*
                         * Displaying error to user.
                         */
                        micro.windows.info:x:/..catch/*/message?value
                          class:micro-windows-info warning

    /*
     * Main wrapper for displaying existing users.
     */
    div
      class:col-50
      widgets
        div
          class:shaded rounded air-inner
          widgets
            micro.widgets.grid:peeples-grid
              class:hover
              columns
                Username
                Role
                Delete
                  style:"float:right;"
              rows

            /*
             * Pager button wrapper.
             */
            div
              class:right
              widgets
                div
                  .offset:x:/../*(/offset|/.defaults/*/offset)/$?value
                  class:strip
                  style:"display:inline-block;"
                  widgets
                    button
                      innerValue:&lt;
                      oninit

                        /*
                         * Checking if [.offset] is zero, and if so, disabling
                         * the previous button.
                         */
                        p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                          .offset
                        get-widget-property:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                          .offset
                        if:x:/@get-widget-property/*/*?value.int
                          =:int:0

                          /*
                           * Disabling button.
                           */
                          set-widget-property:x:/../*/_event?value
                            disabled

                      onclick

                        /*
                         * Paging to previous page.
                         */
                        p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                          .offset
                        get-widget-property:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                          .offset
                        -:x:/@get-widget-property/*/*?value.int
                          _:int:10

                        /*
                         * For simplicity, simply invoking main launch file again,
                         * now with offset incremented by 10.
                         *
                         * TODO: This should seriously be considered for refactoring!!
                         */
                        clear-widget:peeples-main-container
                        eval-x:x:/+/*
                        micro.evaluate.file:@PEEPLES/manage-users-widget.hl
                          offset:x:/@-?value

                    button
                      innerValue:&gt;
                      onclick

                        /*
                         * Paging to next page.
                         */
                        p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                          .offset
                        get-widget-property:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                          .offset
                        +:x:/@get-widget-property/*/*?value.int
                          _:int:10

                        /*
                         * For simplicity, simply invoking main launch file again,
                         * now with offset incremented by 10.
                         *
                         * TODO: This should seriously be considered for refactoring!!
                         */
                        clear-widget:peeples-main-container
                        eval-x:x:/+/*
                        micro.evaluate.file:@PEEPLES/manage-users-widget.hl
                          offset:x:/@+?value
